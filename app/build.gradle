apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"
apply plugin: 'kotlin-parcelize'

android {
    compileSdkVersion 31
    buildToolsVersion = '29.0.3'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29

        renderscriptTargetApi 29//must match target sdk and build tools
        vectorDrawables.useSupportLibrary = true

        applicationId "code.name.monkey.retromusic"
        versionCode 10520
        versionName '5.0.0' + "_" + getDate()

        buildConfigField("String", "GOOGLE_PLAY_LICENSING_KEY", "\"${getProperty(getProperties('../public.properties'), 'GOOGLE_PLAY_LICENSE_KEY')}\"")
    }
    signingConfigs {
        release {
            Properties properties = getProperties('retro.properties')
            storeFile file(getProperty(properties, 'storeFile'))
            keyAlias getProperty(properties, 'keyAlias')
            storePassword getProperty(properties, 'storePassword')
            keyPassword getProperty(properties, 'keyPassword')
        }
    }
    buildTypes {
        release {
            //debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
        debug {
            applicationIdSuffix '.debug'
            versionNameSuffix ' DEBUG'
        }
    }

    buildFeatures{
        viewBinding true
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/java.properties'
    }
    lintOptions {
        disable 'MissingTranslation'
        disable 'InvalidPackage'
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }

    configurations.all {
        resolutionStrategy.force 'com.google.code.findbugs:jsr305:1.3.9'
    }
}

def getProperties(String fileName) {
    final Properties properties = new Properties()
    def file = rootProject.file(fileName)
    if (file.exists()) {
        file.withInputStream { stream -> properties.load(stream) }
    }
    return properties
}

static def getProperty(Properties properties, String name) {
    return properties.getProperty(name) ?: "$name missing"
}

static def getDate() {
    new Date().format('MMddyyyyss')
}


dependencies {
    implementation project(':appthemehelper')
    implementation AndroidX.gridLayout
    implementation AndroidX.cardView
    implementation AndroidX.appCompat
    implementation AndroidX.annotation
    implementation AndroidX.constraintLayout
    implementation AndroidX.recyclerView
    implementation AndroidX.preferenceKtx
    implementation AndroidX.core.ktx
    implementation AndroidX.paletteKtx

    //Cast Dependencies
    implementation AndroidX.mediaRouter
    implementation 'com.google.android.gms:play-services-cast-framework:_'
    //WebServer by NanoHttpd
    implementation "org.nanohttpd:nanohttpd:_"

    def nav_version = "2.4.0-alpha07"
    implementation AndroidX.navigation.runtimeKtx
    implementation AndroidX.navigation.fragmentKtx
    implementation AndroidX.navigation.uiKtx

    def room_version = "2.3.0"
    implementation AndroidX.room.runtime
    implementation AndroidX.room.ktx
    kapt AndroidX.room.compiler

    def lifecycle_version = "2.3.1"
    implementation AndroidX.lifecycle.viewModelKtx
    implementation AndroidX.lifecycle.liveDataKtx
    implementation AndroidX.lifecycle.commonJava8

    implementation Google.android.play.coreKtx
    implementation Google.android.material

    def retrofit_version = '2.9.0'
    implementation Square.retrofit2
    implementation Square.retrofit2.converter.gson
    implementation Square.okHttp3.loggingInterceptor

    def material_dialog_version = "3.3.0"
    implementation "com.afollestad.material-dialogs:core:_"
    implementation "com.afollestad.material-dialogs:input:_"
    implementation "com.afollestad.material-dialogs:color:_"
    implementation "com.afollestad.material-dialogs:bottomsheets:_"
    //noinspection GradleDependency
    implementation 'com.afollestad:material-cab:_'

    implementation Kotlin.stdlib.jdk8

    def kotlin_coroutines_version = "1.5.2-native-mt"
    implementation KotlinX.coroutines.core
    implementation KotlinX.coroutines.android

    def koin_version = "2.1.5"
    implementation "org.koin:koin-core:_"
    implementation "org.koin:koin-androidx-viewmodel:_"

    implementation 'com.github.bumptech.glide:glide:_'
    kapt 'com.github.bumptech.glide:compiler:_'
    implementation 'com.github.bumptech.glide:okhttp3-integration:_'

    implementation 'com.h6ah4i.android.widget.advrecyclerview:advrecyclerview:_'

    implementation 'org.eclipse.mylyn.github:org.eclipse.egit.github.core:_'
    implementation 'org.bitbucket.ijabz:jaudiotagger:_'
    implementation 'com.anjlab.android.iab.v3:library:_'
    implementation 'com.r0adkll:slidableactivity:_'
    implementation 'com.heinrichreimersoftware:material-intro:_'
    implementation 'com.github.dhaval2404:imagepicker:_'
    implementation 'me.zhanghai.android.fastscroll:library:_'
    debugImplementation 'com.amitshekhar.android:debug-db:_'
}

apply from: '../spotless.gradle'
