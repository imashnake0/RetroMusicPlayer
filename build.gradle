// TODO: Move this to libs.versions.toml.
buildscript {
    ext {
        lifecycle_version='2.5.0'
        navigation_version = '2.5.0'
        mdc_version = '1.7.0-alpha02'
        preference_version = '1.2.0'
        appcompat_version = '1.4.2'
        core_version='1.8.0'
    }
}

plugins {
    id 'com.android.application' version '7.2.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.10' apply false
    id 'androidx.navigation.safeargs' version '2.5.0' apply false
    id "com.github.ben-manes.versions" version "0.42.0"
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

tasks.named("dependencyUpdates").configure {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}